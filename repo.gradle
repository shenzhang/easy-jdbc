group = 'com.github.shenzhang'
version = '0.1.3-SNAPSHOT'
//version = '0.1.1'
description = 'a persistence java tool based on spring-jdbc'

apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            def repositoryUrl = 'http://localhost:8081/nexus/content/repositories/releases'
            def snapshotRepositoryUrl = 'http://localhost:8081/nexus/content/repositories/snapshots'
            def username = repoUser
            def password = repoPassword

            if (profile == 'release') {
                repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

                if (project.version.endsWith('SNAPSHOT')) {
                    error "Please change arifact verion to release convention, current verions: $version"
                }

                if (!project.hasProperty('ossUser') || !project.hasProperty('ossPassword')) {
                    error 'Please provide OSS credential [ossUser & ossPassword] to your gradle.properties'
                }

                username = ossUser
                password = ossPassword
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: repositoryUrl) {
                authentication(userName: username, password: password)
            }

            snapshotRepository(url: snapshotRepositoryUrl) {
                authentication(userName: username, password: password);
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://github.com/shenzhang/easy-jdbc'

                scm {
                    url 'https://github.com/shenzhang/easy-jdbc.git'
                    connection 'https://github.com/shenzhang/easy-jdbc.git'
                    developerConnection 'https://github.com/shenzhang/easy-jdbc.git'
                }

                licenses {
                    license {
                        name 'MIT Licence'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'zshen'
                        name 'Zhang Shen'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}

def error(message) {
    throw new GradleScriptException(message, null)
}
