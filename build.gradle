group = 'com.github.shenzhang'
version = '1.0.0-SNAPSHOT'
description = 'a persistence java tool based on spring-jdbc'

ext {
    springVersion = '4.0.3.RELEASE'
    profile = defaultProperty('profile', 'postgresql')
}

buildscript {
    repositories {
        maven { url 'http://127.0.0.1:8081/nexus/content/groups/public' }
        jcenter()
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    repositories {
        maven { url 'http://127.0.0.1:8081/nexus/content/groups/public' }
        jcenter()
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

dependencies {
    compile(
            'commons-logging:commons-logging:1.1.3',
            'org.slf4j:slf4j-api:1.7.5',
            'org.slf4j:jcl-over-slf4j:1.7.5',
            'org.slf4j:slf4j-log4j12:1.7.6',
            'com.google.guava:guava:18.0',
            'log4j:log4j:1.2.17',

            'commons-beanutils:commons-beanutils:1.9.2',
            "org.springframework:spring-jdbc:$springVersion"
    )

    testCompile(
            'junit:junit:4.11',
			'org.easymock:easymock:3.2',

            "org.springframework:spring-test:$springVersion",
            "org.springframework:spring-context:$springVersion",

            'postgresql:postgresql:9.1-901.jdbc4',
            'mysql:mysql-connector-java:5.1.34'
    )
}

sourceSets {
    test {
        resources {
            srcDir "config/$profile"
        }
    }
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

def defaultProperty(name, defaultValue) {
    hasProperty(name) ? property(name) : defaultValue
}

apply from: 'database.gradle'
apply from: 'repo.gradle'